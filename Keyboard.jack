// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int keyboard;

    /** Initializes the keyboard. */
    function void init()
    {
        let keyboard = 24576;
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed()
    {
        var char key;
        let key = Memory.peek(keyboard);
        return key;
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar()
    {
        var char keyPress;
        while( Keyboard.keyPressed() = 0)
        {
            // exits the loop when a key is pressed
        }
        let keyPress = Keyboard.keyPressed();
        while (~(0 = Keyboard.keyPressed()))
        {
            // exits the loop when key was stopped being pressed
        }
        do Output.printChar(keyPress); // echoes the key to the screen
        return keyPress; // returns the character
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message)
    {
        var String input;
        var char keyPress;
        let input = String.new(100);
        do Output.printString(message);

        let keyPress = Keyboard.readChar();
        while(~( keyPress = String.newLine() ))
        {
            if(~(String.backSpace() = keyPress)) // handles backspace instances
            {
                do input.appendChar(keyPress);
            }
            else
            {
                do input.eraseLastChar(); // removes a char in case a backspace was pressed
            }
            let keyPress = Keyboard.readChar(); // handles the keypress and release
        }
        do Output.printString(input);
        do Output.println();
        return input;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message)
    {
        var String input;
        let input = Keyboard.readLine(message);
        return input.intValue();
    }
}
