// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoToThe;
    static int divisionHelper;

    /** Initializes the library. */
    function void init()
    {
        let twoToThe = Array.new(16);
        do Math.setTwoToThe();
        return;
    }

    /** Initializes twoToThe Array with relevant values. */
    function void setTwoToThe()
    {
        var int i, val;
        let i = 0;
        let val = 1;
        while( i < 16 )
        {
            let twoToThe[i] = val;
            let val = val + val;
            let i = i + 1;
        }
        return;
    }

    /** returns true if the i'th element is 1 in the binary representation of integer x */
    function boolean bit(int x, int i)
    {
        return ~((x & twoToThe[i]) = 0);
    }

    /** Returns the absolute value of x. */
    function int abs(int x) 
    {
        if( x < 0)
        {
            return -x;
        }
        return x;
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y)
    {
        // declare local vars
        var int shiftX, sum, iter;
        // set local vars
        let shiftX = x;
        let sum = 0;
        let iter = 0;
        // iteration
        while(iter < 16)
        {
            if(Math.bit(y, iter))
            {
                let sum = sum + shiftX;
            }
            //shift shiftX by 1 - multiply by 2 in decimal is shift left by 1 in binary
            let shiftX = shiftX + shiftX;
            let iter = iter + 1;
        }
        return sum;
    }

    function int divideHelper(int x, int y)
    {
        var int q;
        // base case
        if( (y > x) | (y < 0) )
        {
            return 0;
        }
        let q = Math.divideHelper(x, y+y);
        if (y > divisionHelper)
        {
            return (q+q);
        }
        else
        {
            let divisionHelper = divisionHelper - y;
            return (1+q+q);
        }
    }


    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y)
    {
        var int division;
        // Handling a neg/pos or pos/neg case
        var int isNegX, isNegY;
        let isNegX = 0;
        let isNegY = 0;
        if( x < 0 )
        {
            let isNegX = 1;
            let x = Math.abs(x);
        }
        if( y < 0)
        {
            let isNegY = 1;
            let y = Math.abs(y);
        }
        // calculation and recursive call
        let divisionHelper = x;
        let division = Math.divideHelper(x, y);
        // Handling a neg/pos or pos/neg case
        if ((isNegX +isNegY) = 1)
        {
            return -division;
        }
        return division;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x)
    {
        var int y, index, temp, calc;
        let y = 0;
        let index = 7; //(16/2)-1
        while (~(index < 0))
        {
            let temp = y +  twoToThe[index];
            let calc = temp * temp;
            if(~( calc > x) )// (y+2^index)^2 <= x
            {
                if( calc > 0) // (y+2^index)^2 > 0 for overflow purposes
                {
                    let y = y + twoToThe[index];
                }
            }
            let index = index - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b)
    {
        if( b > a )
        {
            return b;
        }
        return a;
    }

    /** Returns the smaller number. */
    function int min(int a, int b)
    {
        if( b < a )
        {
            return b;
        }
        return a;
    }
}
